# Copyright 2005-2007 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

This test exists for the purpose of exercising the general file-descriptor-stream code in E-on-CL.

  ? pragma.enable("dot-props")
  > def [ALL, NOW, ADVANCE, QUERY, ELEMENTS] := \
  >     [EIO::ALL, EIO::NOW, EIO::ADVANCE, EIO::QUERY, EIO::ELEMENTS]
  > null

XXX to be reviewed: makePipe should probably not be in the privilegedScope top level, but we have no suitable namespace for it as yet

  x ? makePipe
  x # value: <Far ref>

  ? interp.waitAtTop(def pipe := makePipe <- run(throw))
  
  ? def [i, o] := pipe
  # value: [<-<system pipe>, -><system pipe>]
  
  ? [i.available(), o.available()]
  # value: [0, 4096]

  ? o.write([9])
  ? o.flush()
  ? interp.waitAtTop(i.whenAvailable(1, fn{}))
  
  ? [i.available(), i.read(0, ALL)]
  # value: [1, [9]]
  
closure propagation, write-to-read
  
  ? o.close(); null
  ? interp.waitAtTop(i.whenAvailable(1, fn{}))

  ? i.terminates()
  # value: true

closure propagation, read-to-write

  ? interp.waitAtTop(def pipe := makePipe <- run(throw))
  
  ? def [i, o] := pipe
  # value: [<-<system pipe>, -><system pipe>]
  
  ? i.close(); null

  ? o.write([10]); o.flush() # XXX done solely to trigger a write which will notice the failure -- necessary?
  
  ? interp.waitAtTop(o.terminates())

  ? o.terminates()
  # value: <ref broken by problem: error 32 (Broken pipe)>
  XXX review wording of error (this one is inherited from SBCL errno handling)

closing write end does not discard data

  ? interp.waitAtTop(def pipe := makePipe <- run(throw))
  ? def [i, o] := pipe; null
  
  attempt to stuff all buffers
  ? var written := 0
  > def t := timer.now() + 2000
  > while (def a := o.available(); a > 0 || timer.now() < t) {
  >   written += a
  >   o.write([233] * a)
  >   o.flush()
  > }
  > o.close()
  > trace(`$written elements stuffed`)
  > null
  
  ? var read := 0
  > def r() :any {
  >  trace(`$read`)
  >  if (i.remaining() != 0) {
  >    return i.whenAvailable(1, fn { read += i.read(0, ALL).size(); r() })
  >  }
  > }
  > interp.waitAtTop(r())
  
  ? [i.terminates(), read - written]
  # value: [true, 0]
# Copyright 2005 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.enable("easy-return")
pragma.disable("explicit-result-guard")
pragma.enable("dot-props")
pragma.enable("verb-curry")
pragma.enable("accumulator")
pragma.enable("trinary-define")

def mapStream := <import:org.cubik.cle.io.mapStream>

def mapV implements DeepFrozen {
  to run(f, coll) { return accum [] for v in coll {_.with(f(v))}}}

def GSymbol := any
def GProduction := any[Tuple[any, List[any]],
                       Tuple[any, List[any], any]]

def makeLALR1ParserAuthor { # implements DeepFrozen
  to run(lisp) {

    # XXX duplicated code from IPAuthor.emaker
    def intern := lisp["CL", "INTERN"]::"function"
    def read := lisp["CL", "READ-FROM-STRING"]::"function"
    def l__quasiParser {
      to valueMaker(t) {
        return def vm {
          to substitute(values) { return read(simple__quasiParser.valueMaker(t).substitute(values)) }
        } 
      }
    }
    def <kw> { to get(k :String) { return intern(k, "KEYWORD") }}
    
    { def asdfOperate := lisp["ASDF", "OPERATE"]::"function"
      def load_op := intern("LOAD-OP", "ASDF")
      
      # XXX :verbose nil for asdf loads
      asdfOperate(load_op, intern("YACC", "KEYWORD"))
    }
    
    def clCoerce := lisp["CL", "COERCE"]::"function"
    def makeSymbol := lisp["CL", "MAKE-SYMBOL"]::"function"
    def fdefinition := lisp["CL", "FDEFINITION"]::"function"
    def eToLispFunction := lisp["E.KNOT", "E-TO-LISP-FUNCTION"]::"function"
    def eToLispMVFunction := lisp["E.KNOT", "E-TO-LISP-MV-FUNCTION"]::"function"
    def yaccMakeParser := lisp["YACC", "MAKE-PARSER"]::"function"
    def yaccMakeGrammar := lisp["YACC", "MAKE-GRAMMAR"]::"function"
    def yaccMakeProduction := lisp["YACC", "MAKE-PRODUCTION"]::"function"
    def yaccParseWithLexer := lisp["YACC", "PARSE-WITH-LEXER"]::"function"
    def YaccParseErrorT := lisp.unsealingConditionGuard(l`yacc:yacc-parse-error`)
       
    def ypeTerminal := lisp["YACC", "YACC-PARSE-ERROR-TERMINAL"]::"function"
    def ypeExpectedTerminals := lisp["YACC", "YACC-PARSE-ERROR-EXPECTED-TERMINALS"]::"function"

    def mapToSymbol(input, map) {
      return map.fetch(
        input, 
        thunk {map[input] := makeSymbol(E.toString(input))})
    }

    def makeLALR1Parser {
      to run(label :String, 
             startSymbol :GSymbol,
             terminals :List[GSymbol],
             productions :List[GProduction],
             [=> precedence :List[GSymbol] := [],
              => tokenFunction]) { 
             # , optWarningHandler
        
        def symbolsFlex := [].asMap().diverge()
        
        def convertSymbol(i) { return mapToSymbol(i, symbolsFlex) }
        
        def terminalsSet := mapV(convertSymbol, terminals).asSet()
             
        def yp := yaccMakeParser(
          yaccMakeGrammar(
            <kw:NAME>, makeSymbol(label), 
            <kw:START-SYMBOL>, convertSymbol(startSymbol), 
            <kw:TERMINALS>, clCoerce(terminalsSet.getElements(), l`cl:list`),
            <kw:PRECEDENCE>, clCoerce(mapV(convertSymbol, precedence), l`cl:list`),
            <kw:PRODUCTIONS>, clCoerce(accum [] for [fs, derives] + actionSection in productions {
              _.with(yaccMakeProduction(
                       convertSymbol(fs),
                       clCoerce(mapV(convertSymbol, derives), l`cl:list`),
                       <kw:ACTION>,
                       switch (actionSection) {
                         match [action] { eToLispFunction(action) }
                         match [] { fdefinition(l`cl:vector`) }
                       }))
             }, l`cl:list`)))
        
        def parser {
          to __printOn(tw :TextWriter) {
            tw.write("<")
            tw.print(label)
            tw.write(" LALR(1) parser>")
          }
          
          to parse(stream) {
            # XXX error path
            try {
              return yaccParseWithLexer(
                eToLispMVFunction(thunk {
                  if (stream.remaining() != 0) {
                    def [sym, value] := tokenFunction(stream.read(1, 1)[0])
                    def ysym := convertSymbol(sym)
                    if (!terminalsSet.contains(ysym)) {
                      throw(`${E.toQuote(sym)} is not a terminal symbol`)
                    }
                    [ysym, value]
                  } else {
                    [null, null]
                  }
                }), 
                yp)
            } catch p :YaccParseErrorT {
              # XXX provision for source spans
              return Ref.broken(`parse error: expected one of ${E.toQuote(clCoerce(ypeExpectedTerminals(p), l`cl:vector`))}, got ${E.toQuote(ypeTerminal(p))}`)
            }
          }
        }
        
        return parser
      }
      
      to getLalr1__quasiParser() {
        # XXX move this to a separate emaker
        
        def listAction(item, list) { return [item] + list }
        
        def lex(textIn) {
          # XXX get a better lexer library/pattern
          var text := textIn.read(EIO::ALL, EIO::ALL)
          #traceln(`text is ->$text<-, match ${text =~ rx`(?s)"(@mid.*)`} ->$mid<-`)
          def tokens := [].diverge()
          
          while (!(text <=> "")) {
            switch (text) {
              
              match rx`(?s)\s+(@r.*)` { text := r }
  
              match rx`(?s)\.(@r.*)` {
                text := r
                tokens.push(["end", null])
              }
  
              match rx`(?s):=(@r.*)` {
                text := r
                tokens.push(["derives", null])
              }
  
              match rx`(?s)=>(@r.*)` {
                text := r
                tokens.push(["yields", null])
              }
  
              match rx`(?s)(@t[$$@@])\{(@n[0-9+]+)\}(@r.*)` {
                text := r
                tokens.push(["Quasi" + t, __makeInt(n)])
              }
            
              match rx`(?s)(@t[a-zA-Z_][a-zA-Z_0-9-]*)(@r.*)` { 
                # XXX Unicode character categories
                # XXX the character syntax here was arbitrarily copied from the TermLexer
                text := r
                tokens.push(["symbol", t])
              }

              match rx`(?s)"(@mid.*)` {
                def lexerRet := __return
                def rx`(?s)(@t(?:[^@@$$"]|@@@@|$$$$|\\[\\"])*)"(@r.*)` := \
                  (mid, def fail(p) {
                          lexerRet(Ref.broken(`unclosed/malformed string literal ($p): "$mid`))
                        })
                # XXX doesn't handle \<doesn't need quoting>
                text := r
                def unquote := t.replaceAll("@@", "@").replaceAll("$$", "$").replaceAll("\\\\", "\\").replaceAll("\\\"", "\"")
                tokens.push(["string", t])
              }

              match _ { return Ref.broken(`unexpected grammar token: $text`) }
            }
          }
          return tokens.snapshot().asStream()
        }
        
        def metaparser := makeLALR1Parser(
          "grammar",
          "grammar",
          ["string", "derives", "yields", "symbol", "Quasi$", "end"],
          [["grammar", ["string", "derives", "item", "end", "productions"],
              def _(name, _, start, _, qproductions) {
                
                def terminalSymbolsFlex := [].diverge()
                def productions := 
                  accum [] for [left, right] + actionList in qproductions {_.with(
                    # collect the indexes of values resulting from terminals,
                    # which have no actions and therefore don't have nor need
                    # our qpIntermediate gimmick
                    var leaves := [].asSet()
                    
                    for argPosition => quasiSym :int in right {
                      terminalSymbolsFlex.push(quasiSym)
                      leaves with= argPosition
                    }
                    # cl-yacc doesn't allow parameterizing the actions after
                    # the grammar has been built, so the quasiStubAction
                    # generates closures which are supplied with the 
                    # ValueMaker#substitute args after the parse finishes
                    def quasiStubAction {
                      match [=="run", args] {
                        def qpIntermediate(qSubArgs) {
                          def userArgs := \
                            accum [] for i => parg in args { _.with(
                              if (leaves.contains(i)) {
                                # a value from a terminal
                                parg
                              } else {
                                # parg is another qpIntermediate
                                parg(qSubArgs)
                              }
                            )}
                          return \
                            if (actionList =~ [actionFnIndex]) {
                              E.call(qSubArgs[actionFnIndex], "run", userArgs)
                            } else {
                              userArgs
                            }
                        }
                      }
                    }
                    [left, right, quasiStubAction]
                    
                  )}
                
                def terminalSymbols := terminalSymbolsFlex.snapshot()
                
                def ePlainParser := makeLALR1Parser(name, start, terminalSymbols, productions, ["tokenFunction" => __identityFunc])
                
                def valueMaker {
                  to substitute(args :List) {
                    def terminalMap := accum [].asMap() for x in terminalSymbols { _.with(args[x], x) }
                    require(terminalMap.size() == terminalSymbols.size(), thunk {`duplicate terminals in $args`}) # this could be done more simply with a strict 'with'
                    
                    def quasiResultParser {
                      to __printOn(tw :TextWriter) {
                        tw.write("<")
                        tw.print(name)
                        tw.write(" quasi-defined LALR(1) parser>")
                      }
                    
                      to parse(stream) {
                        def intermediate := ePlainParser.parse(mapStream(any, stream,
                          def quasiParserSymbolizer(token) {
                            def [sym, val] := token
                            return [terminalMap[sym], val]
                          }))
                        
                        return intermediate.run(args)
                      }
                    }
                    
                    return quasiResultParser
                  }
                }
                return valueMaker
              }],
           ["productions", [], thunk{[]}],
           ["productions", ["production", "productions"],
              listAction],
           ["production", ["symbol", "derives", "seq", "end"],
              def _(symbol,_,seq,_) {
                return [symbol, seq]
              }],
           ["production", ["symbol", "derives", "seq", "yields", "Quasi$", "end"],
              def _(symbol,_,seq,_,actionIndex,_) {
                return [symbol, seq, actionIndex]
              }],
           ["seq", [], thunk{[]}],
           ["seq", ["item", "seq"], listAction],
           ["item", ["symbol"], __identityFunc],
           ["item", ["Quasi$"], __identityFunc]],
          ["tokenFunction" => __identityFunc])
        
        def lalr1__quasiParser {
          to valueMaker(template :String) {
            return metaparser.parse(lex(template.asStream()))
          }
        }
        
        return lalr1__quasiParser
      }
    }
    
    return makeLALR1Parser
  }
}
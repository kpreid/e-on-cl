# Copyright 2005-2007 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")
pragma.enable("accumulator")

#def vmMakers := <elang:evm.*>
def root := <import:org.cubik.cle.root>

def makeKernelECopyVisitor implements DeepFrozen { # XXX write tests and documentation for this
  to __printOn(out :TextWriter) {
    out.print("<import:org.erights.e.elang.visitors.makeKernelECopyVisitor>")
  }
  
  to run(self) {
    def kernelECopyVisitor extends root[self] {
      to run(node) {
        return switch (node) {
          match list :List {
            accum [] for x in list { _.with(self.run(x)) }
          }
          match ==null { null }
          match _ {
            node.welcome(self)
          }
        }
      }
    
      match [`visit@kind`, [optOriginal] + args] {
        # xxx package-loaders are currently not DeepFrozen
        # def maker := vmMakers[kind]
        def maker := <import>["org.erights.e.elang.evm." + kind]
        
        def subnodeFlags := maker.getParameterSubnodeFlags()
        
        def left := [if (optOriginal != null) {optOriginal.getOptSpan()}]
        # xxx preserve scope layout arg, if it ever becomes real
        def right := [null]
        
        E.call(maker, "run",
          accum left for i => arg in args { _.with(
            if (subnodeFlags[i]) {
              self.run(arg)
            } else {
              arg
            }
          )} + right
        )
      }
    }
    return kernelECopyVisitor
  }
}
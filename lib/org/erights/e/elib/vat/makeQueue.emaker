# Copyright 2005-2007 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")

# XXX should we instead export the CL-implemented queue that our non-threaded vat implementation used?

/** Java-E rune compatibility. */
def makeQueue {
  to run() { return makeQueue(any) }
  
  to run(Element) {
    # XXX use conses since that's what we're really doing
    
    var inn := null
    var out := null
    
    def queue {
      to enqueue(new :Element) :void {
        inn := [new, inn]
      }
      
      to optDequeue() :nullOk[Element] {
        if (out == null) {
          while (inn =~ [elem, newIn]) {
            inn := newIn
            out := [elem, out]
          }
        }
        return if (out =~ [elem, newOut]) {
          out := newOut
          elem
        }
      }
    }
    return queue
  }
}
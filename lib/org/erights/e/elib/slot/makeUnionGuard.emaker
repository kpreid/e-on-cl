# Copyright 2005-2006 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")

def EventuallyDeepFrozen := DeepFrozen."eventually"()

interface UnionGuard guards UnionGuardStamp {}

def makeUnionGuard implements DeepFrozen {
  to run(subGuards) {
    def unionGuard implements EventuallyDeepFrozen, UnionGuardStamp {
      method __optSealedDispatch(brand) :any {
        if (brand == EventuallyDeepFrozen.getPeekBrand()) {
          EventuallyDeepFrozen.getPeekSealer().seal(meta.getState())
        }
      }

      to __printOn(out :TextWriter) {
        out.print("any")
        out.quote(subGuards)
      }
      
      # xxx this flattening can no longer be done trivially
      # to or(other) {
      #   return <elib:slot.makeUnionGuard>(subGuards.with(other))
      # }
      
      to coerce(specimen, optEjector) {
        for guard in subGuards {
          return guard.coerce(specimen, __continue)
        }
        # XXX this is unlike Java-E's UnionGuard, but provides a more informative message, but hides information about individual failures. fix this once we have fancy exception structure.
        throw.eject(optEjector, `doesn't match any of $subGuards`)
      }
      
      to getChoices() { return subGuards }
    }
    
    return unionGuard
  }
  
  to "match__run/1"(specimen, ejector) {
    def ofKind := UnionGuard.coerce(specimen, ejector)
    return [ofKind.getChoices()]
  }
}

# XXX unneeded check
# XXX flatten check

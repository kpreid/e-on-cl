# Copyright 2005-2007 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")

pragma.enable("accumulator")

def simplifyFQName := <import:org.cubik.cle.prim.simplifyFQName>

def ConstMap := <import:org.cubik.cle.prim.ConstMap>

interface MapGuard guards MapGuardStamp {}

def Map0 implements MapGuardStamp, DeepFrozen, ExitViaHere {
  to __printOn(out :TextWriter) {
    out.print("Map")
  }

  to coerce(specimen, optEjector) {
    return ConstMap.coerce(specimen, optEjector)
  }

  to get(keyGuard, valueGuard) {
    def Map1 implements MapGuardStamp {

      to coerce(var specimen, optEjector) {
        specimen := Map0.coerce(specimen, optEjector)
        
        # XXX we need a more efficient way of doing this which doesn't require accumulating a new map for non-coercing cases
        return accum [].asMap() for subKey => subValue in specimen {
          _.with(keyGuard  .coerce(subKey,   optEjector),
                 valueGuard.coerce(subValue, optEjector))
        }
      }
      
      to getKeyGuard()   { return keyGuard }
      to getValueGuard() { return valueGuard }
      to getValGuard()   { return valueGuard } # XXX EoJ compatibility
    
      to __printOn(out :TextWriter) {
        out.print("Map[")
        out.quote(keyGuard)
        out.print(", ")
        out.quote(valueGuard)
        out.print("]")
      }
    }
    
    return Map1
  }

  to getKeyGuard()   { return any }
  to getValueGuard() { return any }
  
  to "match__get/2"(specimen, ejector) {
    def ofKind := MapGuard.coerce(specimen, ejector)
    return [ofKind.getKeyGuard(), ofKind.getValueGuard()]
  }
  
  to getTheTrivialValue() { return [].asMap() }
}
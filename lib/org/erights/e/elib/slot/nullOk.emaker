# Copyright 2005 Kevin Reid, under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.enable("easy-return")
pragma.disable("explicit-result-guard")

def nullOk0 extends __makeGuard(nullOk0) {
  to __printOn(out :TextWriter) :void {
    out.print("nullOk")
  }
  
  to coerce(specimen, optEjector) :any {
    if (specimen != null) {
      throw.eject(optEjector, "must be null")
    }
  }
  
  to getTheTrivialValue() { return null }
  
  to get(subGuard) :Guard {
    def nullOk1 extends __makeGuard(nullOk1) {
      to __printOn(out :TextWriter) :void {
        # XXX print this explicitly as E-syntax sugared "get" call
        out.quote(nullOk0, [subGuard])
      }
    
      to coerce(specimen, optEjector) {
        if (specimen == null) {
          return null
        } else {
          return subGuard.coerce(specimen, optEjector)
        }
      }
    }
    return nullOk1
  }
}
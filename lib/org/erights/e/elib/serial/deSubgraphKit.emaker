#!/usr/bin/env rune

# Copyright 2003 Hewlett Packard, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

# copied 2006-05-10 from 0.8.36n by kpreid, modified:
#   * removed uses of Guard sugar verbs or/1 and get/0

def Uncaller := <type:org.erights.e.elib.serial.Uncaller>

def deASTKit := <elib:serial.deASTKit>
def DEBuilderOf := <elib:serial.DEBuilderOf>
def deSrcKit := <elib:serial.deSrcKit>
def makeCycleBreaker := <elib:tables.makeCycleBreaker>
def makeUncaller := <elib:serial.makeUncaller>

def defaultUncallers := makeUncaller.getDefaultUncallers()

# See comment on getMinimalScope() below.
def minimalScope := [
    "null"              => null,
    "false"             => false,
    "true"              => true,
    "NaN"               => NaN,
    "Infinity"          => Infinity,
    "__makeList"        => __makeList,
    "__identityFunc"    => __identityFunc,
    "__makeInt"         => __makeInt,
    "import__uriGetter" => <import>
]

def defaultScope := [ "type__uriGetter" => <type> ] | minimalScope

def minimalUnscope := makeCycleBreaker.byInverting(minimalScope)

def defaultUnscope := makeCycleBreaker.byInverting(defaultScope)

/**
 * Serialize by generating an expression whose evaluation would produce a
 * reconstruction resembling the original object.
 *
 * @param uncallerList A list of {@link Uncaller}s used as a search path. For a
 *                     given object, calculates what call (if any) would create
 *                     it. Each uncaller is asked until one gives an answer or
 *                     the list is exhausted. Should the list be exhasted,
 *                     recognition terminates with a throw.
 *                     <p>
 *                     uncallerList can be any kind of list -- Flex, Const, RO
 *                     -- since the unevaler snapshot()s it at the beginning of
 *                     each recognize(..).
 * @param unscopeLayout The value => variable-name associations we use as the
 *                   inverse of a scope. Given a value, what's
 *                   the name of the variable (if any) that currently has that
 *                   value? The unscopeLayout should have at least the inverse
 *                   of the bindings defined in the minimalScope.
 *                   <p>
 *                   Since the unscopeLayout generally should be able to map
 *                   from unresolved references (Promises) as keys, it would
 *                   normally be a {@link makeCycleBreaker CycleBreaker}. The
 *                   unscopeLayout can be any kind of CycleBreaker -- Flex,
 *                   Const, RO -- since the unevaler diverge()s it at the
 *                   beginning of each recognize(..).
 * @author Mark S. Miller
 */
def makeUnevaler(uncallerList, unscopeLayout) :near {

    /**
     *
     */
    def unevaler {
        /**
         *
         */
        to recognize(root, builder) :(def Root := builder.getRootType()) {

            def Node := builder.getNodeType()

            def uncallers := uncallerList.snapshot()

            # We will identify temp variables by storing their index (ints)
            # rather than their name (Strings) as unscope-values.
            def unscope := unscopeLayout.diverge()

            def generate

            /**
             * traverse an uncall portrayal
             */
            def genCall(rec, verb :String, args :List) :Node {
                def recExpr := generate(rec)
                var argExprs := []
                for arg in args {
                    argExprs with= generate(arg)
                }
                builder.buildCall(recExpr, verb, argExprs)
            }

            /**
             * When we're past all the variable manipulation.
             */
            def genObject(obj) :Node {
                # scalars are transparent, but can't be uncalled.
                # They are instead translated to literal expressions.
                # The scalars null, true, and false should have already
                # been picked up by the unscope -- they should be in the
                # provided unscopeLayout.
                if (obj =~ i :int)     { return builder.buildLiteral(i) }
                if (obj =~ f :float64) { return builder.buildLiteral(f) }
                if (obj =~ c :char)    { return builder.buildLiteral(c) }

                # Bare strings are transparent and aren't scalars, but
                # still can't be uncalled. Instead, they are also
                # translated into literal expressions
                if (obj =~ twine :Twine && twine.isBare()) {
                    return builder.buildLiteral(twine)
                }

                for uncaller in uncallers {
                    if (uncaller.optUncall(obj) =~ [rec, verb, args]) {
                        return genCall(rec, verb, args)
                    }
                }
                throw(`Can't uneval ${E.toQuote(obj)}`)
            }

            /** Build a use-occurrence of a variable. */
            def genVarUse(varID :any[String, int]) :Node {
                if (varID =~ varName :String) {
                    builder.buildImport(varName)
                } else {
                    builder.buildIbid(varID)
                }
            }

            /**
             * The internal recursive routine that will traverse the
             * subgraph and build a Data-E Node while manipulating the
             * above state.
             */
            bind generate(obj) :Node {
                if (unscope.fetch(obj, fn{}) =~ varID :notNull) {
                    return genVarUse(varID)
                }
                def promIndex := builder.buildPromise()
                unscope[obj] := promIndex
                def rValue := genObject(obj)
                builder.buildDefrec(promIndex+1, rValue)
            }

            builder.buildRoot(generate(root))
        }

        /**
         * A printFunc can be used as an argument in
         * <pre>    interp.setPrintFunc(..)</pre>
         * to be used as the 'print' part of that read-eval-print loop.
         * When using an unevaler's printFunc for this purpose, we have instead
         * a read-eval-uneval loop.
         */
        to getPrintFunc() :near {
            def printFunc(value, out :TextWriter) :void {
                def builder := deASTKit.wrap(deSrcKit.makeBuilder())
                out.print(unevaler.recognize(value, builder))
            }
            return printFunc
        }
    }
}

def defaultRecognizer := makeUnevaler(defaultUncallers, defaultUnscope)


/**
 * Unserializes/evals by building a subgraph of objects, or serializes/unevals
 * by recognizing/traversing a subgraph of objects.
 *
 * @author Mark S. Miller
 */
def deSubgraphKit implements ExitViaHere {

    /**
     * This is the default scope used for recognizing/serializing/unevaling and
     * for building/unserializing/evaling.
     * <p>
     * The minimal scope only has bindings for<ul>
     * <li>the scalars which can't be written literally<ul>
     *     <li><tt>null</tt>
     *     <li><tt>false</tt>
     *     <li><tt>true</tt>
     *     <li>floating point <tt>NaN</tt>. Same as 0.0/0.0
     *     <li>floating point <tt>Infinity</tt>. Same as 1.0/0.0.
     *     </ul>
     *     The additional scalars which can't be written literally are the
     *     negative numbers, including negative infinity. The can instead be
     *     expressed by a unary "-" or by calling ".negate()" on the magnitude.
     * <li><tt>__makeList</tt>. Many things are built from lists.
     * <li><tt>__identityFunc</tt>. Enables the equivalent of JOSS's
     *     <tt>{@link java.io.ObjectOutputStream#replaceObject
     *                replaceObject()}</tt>
     * <li><tt>__makeInt</tt>. So large integers (as used by crypto) can print
     *     in base64 by using <tt>__makeInt.fromString64("...")</tt>.
     * <li><tt>import__uriGetter</tt>. Used to name safe constructor / makers
     *     of behaviors.
     * </ul>
     */
    to getMinimalScope() :near { minimalScope }

    /**
     * XXX For now, it's mostly the same as the minimalScope, but we expect to
     * add more bindings from the safeScope; possibly all of them.
     */
    to getDefaultScope() :near { defaultScope }

    /**
     *
     */
    to getMinimalUnscope() :near { minimalUnscope }

    /**
     * XXX For now, it's the same as the minimalUnscope, but we expect to add
     * more bindings from the safeScope; possibly all of them.
     */
    to getDefaultUnscope() :near { defaultUnscope }

    /**
     *
     */
    to getDefaultUncallers() :List[Uncaller] { defaultUncallers }

    /**
     * Makes a builder which evaluates a Data-E tree in the default scope to a
     * value.
     *
     * @see #getMinimalScope
     */
    to makeBuilder() :near {
        deSubgraphKit.makeBuilder(defaultScope, E)
    }

    /**
     * The caller defaults to E.
     */
    to makeBuilder(scope) :near {
        deSubgraphKit.makeBuilder(scope, E)
    }

    /**
     * Makes a builder which evaluates a Data-E tree in a scope to a value.
     * <p>
     * This <i>is</i> Data-E Unserialization. It is also a subset of E
     * evaluation.
     */
    to makeBuilder(scope, caller) :near {

        # The index of the next temp variable
        var nextTemp := 0

        # The frame of temp variables
        def temps := [].diverge()

        def Node := any
        def Root := any

        def deSubgraphBuilder implements DEBuilderOf(Node, Root) {
            to getNodeType() :near { Node }
            to getRootType() :near { Root }

            to buildRoot(root :Node)        :Root { root }
            to buildLiteral(value)          :Node { value }
            to buildImport(varName :String) :Node { scope[varName] }
            to buildIbid(tempIndex :int)    :Node { temps[tempIndex] }

            to buildCall(rec :Node, verb :String, args :List[Node]) :Node {
                caller.call(rec, verb, args)
            }

            to buildDefine(rValue :Node) :Tuple[Node, int] {
                def tempIndex := nextTemp
                nextTemp += 1
                temps[tempIndex] := rValue
                [rValue, tempIndex]
            }

            to buildPromise() :int {
                def promIndex := nextTemp
                nextTemp += 2
                def [prom,res] := Ref.promise()
                temps[promIndex] := prom
                temps[promIndex+1] := res
                promIndex
            }

            to buildDefrec(resIndex :int, rValue :Node) :Node {
                temps[resIndex].resolve(rValue)
                rValue
            }
        }
    }

    /**
     *
     */
    to getDefaultRecognizer() :near { defaultRecognizer }

    /**
     *
     */
    to makeRecognizer(optUncallers, optUnscope) :near {
        def uncallers := if (null == optUncallers) {
            defaultUncallers
        } else {
            optUncallers
        }
        def unscope := if (null == optUnscope) {
            defaultUnscope
        } else {
            optUnscope
        }
        makeUnevaler(uncallers, unscope)
    }

    /**
     * Uses the default recognizer
     */
    to recognize(root, builder) :(def Root := builder.getRootType()) {
        defaultRecognizer.recognize(root, builder)
    }
}

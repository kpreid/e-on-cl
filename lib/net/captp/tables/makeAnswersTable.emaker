# Copyright 2002 Combex, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ...............

pragma.syntax("0.9")
pragma.enable("function-implements")

def makeCommTable :DeepFrozen := <import:net.captp.tables.makeCommTable>
def Throwable :DeepFrozen := <type:java.lang.Throwable>

def makeAnswersTable() implements DeepFrozen {
    def answersTable extends makeCommTable(answersTable) {
        to __printOn(out :TextWriter) {
            out.print("<answersTable")
            answersTable.printStateOn(out)
            out.print(">")
        }
        to smash(problem :Throwable) :void {
            for i in 1..!(answersTable._getCapacity()) {
                if (!answersTable.isFree(i)) {
                    E.sendOnly(answersTable[i], "__reactToLostClient", [problem])
                }
            }
            super.smash(problem)
        }
    }
    return answersTable
}
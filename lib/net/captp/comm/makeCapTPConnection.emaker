# Copyright 2007 Kevin Reid under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")
pragma.enable("function-implements")
pragma.enable("accumulator")

def map implements DeepFrozen {
  to v(f, coll) {
    return accum [] for v in coll {_.with(f(v))}}
  to kv(f, coll) {
    return accum [] for k => v in coll {_.with(f(k, v))}}
}

# definitions per http://www.erights.org/elib/distrib/captp/types.html
def IncomingPos := -2**31..!2**31
def ExportPos := 1..!2**31
def AnswerPos := -2**31..-1
def ImportPos := 0..!2**31
def WireDelta := 0..255
def MsgCount := 0..!2**63
def MsgName := String
def VatID := String
def SwissBase := int
def SwissNumber := int
def SwissHash := int
def Nonce := int
def OneArgFunc := <elib:util.OneArgFunc>

def DESCS_EXIT := "CapTP_1_descs"

def makeProxy := <elib:ref.makeProxy>
def makeBrand := <elib:sealing.makeBrand>
def Throwable := <type:java.lang.Throwable>

def makeNonceLocator := <import:net.captp.comm.makeNonceLocator>
def makeAnswersTable := <import:net.captp.tables.makeAnswersTable>
def makeExportsTable := <import:net.captp.tables.makeExportsTable>
def makeProxiesTable := <import:net.captp.tables.makeProxiesTable>

def descMakerStandin implements DeepFrozen {}

def makeCapTPConnection implements ExitViaHere, DeepFrozen {
  to run(var outgoingReceiver, var outArgBuilderMaker, swissTable, whenGarbage) {
    def nonceLocator := makeNonceLocator(null,
                                         null,
                                         "fake-id-ctc",
                                         null,
                                         swissTable)
    
    def answers := makeAnswersTable()
    def exports := makeExportsTable()
    def imports := makeProxiesTable()
    def questions := makeProxiesTable()
    
    def termination # resolved when connection is dead
    def remoteNonceLocatorResolutionSlot
    
    def prepareQuestion
    
    def [remoteIncomingPosSealer, remoteIncomingPosUnsealer] := 
      makeBrand("CapTP far ref's remote incomingPos")
    def remoteIncomingPosBrand := remoteIncomingPosSealer.getBrand()

    def deSubgraphKit := <elib:serial.deSubgraphKit> # XXX unnecessarily not DeepFrozen
    def makeCycleBreaker := <elib:tables.makeCycleBreaker> # ditto
    def once := <import:org.erights.e.facet.once> # ditto
    
    def unscope := {
      def u := deSubgraphKit.getDefaultUnscope().diverge()
      u[descMakerStandin] := DESCS_EXIT
      u.snapshot()
    }
    
    /** Uncalls our local NonceLocator being passed out. */
    def nonceLocatorUncaller {
      to optUncall(ref) {
        if (__equalizer.sameYet(ref, nonceLocator)) {
          return [descMakerStandin, "Import", [0]]
        }
      }
    }
    
    def pbcUncaller {
      to optUncall(r) {
        if (r =~ p :pbc && r == p) {
          return p.__optUncall()
        }
      }
    }
    
    /** Uncalls Far refs that are being passed to their home vat. */
    def goingHomeUncaller {
      to optUncall(ref) {
        if (remoteIncomingPosUnsealer.amplify(ref) =~ [pos :IncomingPos]) {
          return [descMakerStandin, "Incoming", [pos]]
        }
      }
    }
    
    def proxyUncaller {
      to optUncall(r) {
        if (Ref.isSelfish(r)) {
          if ((def index := exports.indexFor(r)) != -1) {
            exports.incr(index)
            return [descMakerStandin, "Import", [index]]
          } else {
            def swissNumber := swissTable.getNewSwiss(r)
            return [descMakerStandin, "NewFar", [exports.newFarPos(r), swissNumber.cryptoHash()]]
          }
        }
      }
    }
    
    /** Uncalls promises, assuming that they are either local or from a foreign comm system; that is, they are not subject to 3-vat introduction. */
    def localPromiseUncaller {
      to optUncall(r) {
        if (Ref.isEventual(r) && !Ref.isResolved(r)) {
          def rdrSwissBase := swissTable.nextSwiss()
          def [rdrAnswerPos, rdrRecord] := prepareQuestion(true)
          # XXX we're not using rdrSwissBase for anything; it *should* be extractible from the question proxy so that we can pass it to another vat

          Ref.whenResolvedOnly(r, rdrRecord.reference())          
          return [descMakerStandin, "NewRemotePromise", [
                   exports."bind"(r),
                   rdrAnswerPos,
                   rdrSwissBase]]
        }
      }
    }
    
    # XXX document exit table used
    def encRecognizer := deSubgraphKit.makeRecognizer(
      [goingHomeUncaller,
       nonceLocatorUncaller,
       pbcUncaller,
       localPromiseUncaller,
       proxyUncaller],
      unscope)
    
    /** given an index from an incoming message, look up the corresponding local object */
    def lookupIncoming(index :int) {
      return if (index.isZero()) {
        nonceLocator
      } else if (index.belowZero()) {
        answers[-index]
      } else { # above zero
        exports[index]
      }
    }

    def lookupImport(index :int) {
      return if (index.isZero()) {
        throw("Not proven necessary yet")
        #return remoteNonceLocator
      } else if (index.belowZero()) {
        #questions[-index]
        throw("can't happen: <0 import")
      } else { # above zero
        imports.getProxy(index)
      }
    }

    /** Transform a CapTP message argument object into an outgoing wire message. */
    def outEncode(object) {
      return encRecognizer.recognize(object, outArgBuilderMaker())
    }
    
    def requireLive() {
      if (Ref.isResolved(termination)) {
        throw(`this CapTP connection has been terminated (${Ref.optProblem(termination)})`)
      }
    }
    
    def capTPReceiver {
      to DeliverOnly(recipPos :IncomingPos,
                     verb :MsgName,
                     args :List[any]) :void {
        requireLive()
        E.sendOnly(lookupIncoming(recipPos), verb, args)
      }

      to Deliver(answerPos :AnswerPos,
                 rdr :OneArgFunc,
                 recipPos :IncomingPos,
                 verb :MsgName,
                 args :List[any]) :void {
        requireLive()
        def answer := E.send(lookupIncoming(recipPos), verb, args)
        answers.put(-answerPos, answer, true)
        E.sendOnly(answer, "__whenMoreResolved", [rdr])
      }
      
      to GCExport(exportPos :ExportPos,
                  wireDelta :WireDelta) {
        requireLive()
        exports.decr(exportPos, wireDelta)
        # XXX do we need to do something upon reaching 0?
      }
      
      to GCAnswer(answerPos :AnswerPos) {
        requireLive()
        answers.free(-answerPos)
      }

      to Terminated(problem :Throwable) {
        requireLive()
        
        bind termination := Ref.broken(problem)
        
        # Discard our local references, break our proxies
        exports.smash(problem)
        imports.smash(problem)
        answers.smash(problem)
        questions.smash(problem)
        bind remoteNonceLocatorResolutionSlot := __makeFinalSlot(termination)
        
        # Discard our outgoing references, since we will no longer use them
        outgoingReceiver := null
        outArgBuilderMaker := null
        # XXX review for additional cleanup needed/useful
      }
    }
    
    def makeEventualHandler
    
    def makeProxyRecord(far, position) {
      var wireCount := 0
      var localCount := 0
      def [resolutionSlot, proxySlotResolver] := Ref.promise()
      def handler := makeEventualHandler(position)

      return def proxyRecord {
        to resolver() { return proxySlotResolver }
        
        to makeRedirector() {
          return def redirector(resolution) :void {
            # XXX review: Where's our equivalent, if applicable, of DelayedRedirector?
            proxySlotResolver.resolveRace(&resolution)
          }
        }
        
        /** make a local reference, and increment the wireCount (number of received descriptors for this local entry) */
        to reference() {
          wireCount += 1
          def proxy := makeProxy(handler, resolutionSlot, far)
          localCount += 1
          
          whenGarbage(proxy, once(fn {
            localCount -= 1
            if (localCount.atMostZero()) {
              if (position.belowZero()) {
                # question
                E.sendOnly(outgoingReceiver, "GCAnswer", [position])
                questions.free(-position)
              } else if (position.aboveZero()) {
                # import
                E.sendOnly(outgoingReceiver, "GCExport", [position, wireCount])
                # no imports.free(...) because the other side might reuse this entry
              }
              wireCount := 0
            }
          }))
          return proxy
        }
      }
    }
    
    bind makeEventualHandler(position :vow[IncomingPos]) {
      return def capTPEventualHandler {
        to handleSend(verb, args) {

          def [answerPos, questionRecord] := prepareQuestion(false)
          def redirector := questionRecord.makeRedirector()

          E.sendOnly(outgoingReceiver, "Deliver", [answerPos, outEncode(redirector), position, verb, outEncode(args)])
          
          return questionRecord.reference()
        }
        to handleSendOnly(verb, args) {
          E.sendOnly(outgoingReceiver, "DeliverOnly", [position, verb, outEncode(args)])
        }
        to handleOptSealedDispatch(brand) {
          if (brand == remoteIncomingPosBrand) {
            return remoteIncomingPosSealer.seal(position)
          }
        }
        
        to __printOn(out :TextWriter) {
          out.print("<CapTP proxy handler for ", position, " of ", outgoingReceiver, ">")
        }
      }
    }

    bind prepareQuestion(resolved :boolean) {
      def position := -(questions."bind"(def record := makeProxyRecord(
        resolved,
        position)))
      return [position, record]
    }
    
    def incomingDescMaker {
      to NewFar(importPos :ImportPos,
                swissHash :SwissHash) {
        requireLive()

        imports.put(importPos, makeProxyRecord(
          true,
          importPos))
        
        return imports.getProxy(importPos)
      }
      
      to NewRemotePromise(importPos :ImportPos,
                          rdrPos    :AnswerPos,
                          rdrBase   :SwissBase) {
        requireLive()
        
        # XXX it is suspicious that this record isn't stored anywhere
        def record := makeProxyRecord(
          false,
          importPos)
        
        def redirector := record.makeRedirector()

        swissTable.registerIdentity(redirector, rdrBase)
        answers.put(-rdrPos, redirector, true)
        
        return record.reference()
      }
      
      to Import(importPos :ImportPos) {
        requireLive()
        return lookupImport(importPos)
      }
  
      to Incoming(incomingPos :IncomingPos) {
        requireLive()
        return lookupIncoming(incomingPos)
      }
    }
    
    def buildScope := deSubgraphKit.getDefaultScope().with(DESCS_EXIT, incomingDescMaker)
    
    def capTPBuilderMaker() {
      requireLive()
      return deSubgraphKit.makeBuilder(buildScope)
    }
    
    # The remote nonce locator is a Far reference which is unique to this connection -- XXX this representation will have to be changed for ShutdownOp; see connection.updoc
    def remoteNonceLocator := makeProxy(makeEventualHandler(0), remoteNonceLocatorResolutionSlot, true)
    
    return [capTPReceiver, capTPBuilderMaker, remoteNonceLocator]
  }
}
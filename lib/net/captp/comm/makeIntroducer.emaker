# Copyright 2007 Kevin Reid under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

pragma.syntax("0.9")
pragma.enable("call-pattern")

def scheme := "captp"

def makeSwissTable := <import:net.captp.tables.makeSwissTable>
def asWaterkenBase32 := <import:net.captp.util.asWaterkenBase32>
def now := <import:org.cubik.cle.schedule.now>
def unsignedIntegerCoding := <import:org.cubik.cle.binary.unsignedIntegerCoding>
def makeLocatorUnum := <import:net.captp.comm.makeLocatorUnum>
def makeHub := <import:net.captp.comm.makeHub>

def KeyPair := any # XXX stub

def makeIntroducer implements ExitViaHere, DeepFrozen {
  to makePair(config :Map,
              entropy :any, # XXX interface
              timer :any) { # XXX interface
    
    interface SturdyRef {} # XXX stub
    
    def swissTable := makeSwissTable(entropy, <elib:tables.makeFlexMap>, <elib:tables.makeFlexMap>) # XXX should be weak-key and weak-value maps
    
    def negotiable := ["BOGUS_PLACEHOLDER"]
    
    # resolved when we become identified
    def myVatID
    
    def hub
    
    def locatorUnum := makeLocatorUnum(myVatID, swissTable, hub)
    
    def srMap := [].asMap().diverge() # XXX either make sturdyrefs selfless and remove this table, or make it weak
    def getSturdyRef(vatID, searchPathStr, swissNumber) {
      def key := [vatID, searchPathStr, swissNumber]
      if (srMap.maps(key)) { return srMap[key] }
      def sturdyRef implements SturdyRef {
        to __printOn(out :TextWriter) {
          if (__equalizer.sameYet(vatID, myVatID)) {
            out.write("<sturdyRef to ")
            out.quote(sturdyRef.getRcvr())
            out.write(">")
          } else {
            out.write("<sturdyRef>")
          }
        }
        to getRcvr() {
          # XXX stub search path
          return locatorUnum.getRcvr([], vatID, swissNumber, null)
        }
        to _uriBody() { # XXX should be opaque
          return `//*$vatID@@$searchPathStr/${asWaterkenBase32(swissNumber.toOctetsUnsigned())}`
        }
      }
      srMap[key] := sturdyRef
      return sturdyRef
    }
    
    def introducer {
      to getLocatorUnum() { return locatorUnum }
      to getNetConfig() { throw("XXX no net config object yet") } # XXX stub
      to getVatID() {
        # XXX would it be ok to return the promise?
        if (Ref.isResolved(myVatID)) {
          return myVatID
        } else {
          throw("introducer not yet identified")
        }
      }
      to hasIdentity() { return Ref.isResolved(myVatID) }
      to isOnTheAir() { return Ref.isResolved(hub) }
      to negotiable() { return negotiable } # XXX stub
      
      to newVatIdentity() :KeyPair {
        introducer.setVatIdentity(def keyPair {})
        return keyPair
      }
      to setVatIdentity(keyPair :KeyPair) :void {
        if (Ref.isResolved(myVatID)) {
          throw(`$introducer is already identified`) # XXX stub
        }
        bind myVatID := "foo"
      }
      
      to onTheAir() {
        if (!introducer.isOnTheAir()) {
          if (!introducer.hasIdentity()) {
            introducer.newVatIdentity()
          }
          bind hub := makeHub(def makeConnection {}, def outgoingConnect {})
        }
        return negotiable
      }
      
      to __printOn(out :TextWriter) {
        if (introducer.isOnTheAir()) {
          out.write(`<On the Air $negotiable>`)
        } else {
          out.write("<Off the Air>")
        }
      }
    }
    
    def identityMgr {
       to __printOn(out :TextWriter) {
         out.write("<identityMgr Off the Air>")  # XXX stub
       }
       
       to makeKnown(ref) {
         def myVatID1 := introducer.getVatID() # checks for being identified
         def swissBase := entropy.nextSwiss()
         def swissNumber := swissTable.registerNewSwiss(ref, swissBase)
         def sr := getSturdyRef(myVatID1, "", swissNumber) # XXX non-stub searchpath
         return [sr, def timeout {}, swissBase]
       }
       
       # XXX other ops
    }
    
    def <captp> {
      to get(uriBody :String) {
        def `//*@vatID@@@searchPathStr/@{asWaterkenBase32(swissNumberBytes ? (swissNumberBytes != []))}` \
          exit fn p { throw(`Malformed CapTP URI: ${if (`$p` =~ `problem: @t`) {t} else {p}}`) } \
          := uriBody
        # XXX silly way to turn bytes into int; should be __makeInt.fromDigits(1, swissNumberBytes, 256) once that is implemented
        def swissNumber := unsignedIntegerCoding[swissNumberBytes.size() * 8].takingFrom(swissNumberBytes.asStream(), now)()
        return getSturdyRef(vatID, searchPathStr, swissNumber)
      }
      to sturdyFromURI(uri :String) {
        # XXX unnecessary printing
        # XXX deleting 'problem: ' ought to be done by printing without it
        def `$scheme:@uriBody` \
          exit fn p { throw(`Malformed CapTP URI: ${if (`$p` =~ `problem: @t`) {t} else {p}}`) } \
          := uri
        return <captp>[uriBody]
      }
      
      to sturdyToURI(sr) { # XXX guard
        return `$scheme:${sr._uriBody()}`
      }
      to optUnget(specimen) {
        if (specimen =~ sr :SturdyRef) {
          return sr._uriBody()
        }
      }
      to optUncall(specimen) {
        if (specimen =~ sr :SturdyRef) {
          return [introducer, "get", sr._uriBody()]
        }
      }
    }
    
    return [introducer, identityMgr, <captp>]
  }
}